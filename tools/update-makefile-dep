#!/usr/bin/perl
use v5.10;
use strict;
use warnings;

use autodie qw(open close);

sub process($)
{
    my ($line) = @_;
    return if $line eq '';
    my ($pre, $post) = $line =~ /(.+):(.*)/ or die;
    my @post = split ' ', $post;
    my @result = ();
    for (@post) {
        push @result, "$pre: $_";
    }
    for (sort @result) {
        print "$_\n";
    }
}

my $cc = $ENV{'CC'} // 'gcc';

open STDIN, '-|', $cc, '-MM', glob('*.c');
open STDOUT, '>', 'Makefile.dep';
my $line = '';
while (<STDIN>) {
    my $break = 0;
    chomp;
    if (s/\\$//) {
        $break = 1;
    }
    $line = "$line$_";
    next if $break;
    process($line);
    $line = '';
}
process($line);
close STDIN;
close STDOUT;

# vim:ts=4 sw=4 et
